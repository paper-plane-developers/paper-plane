# UI resources
blueprints = custom_target('blueprints',
  input: files(
    'login.blp',
    'phone_number_input.blp',
    'session_manager.blp',
    'window.blp',

    'components/avatar.blp',
    'components/message_entry.blp',
    'session/content/chat_action_bar.blp',
    'session/content/chat_info_window.blp',
    'session/content/event_row.blp',
    'session/content/message_row/document/mod.blp',
    'session/content/message_row/photo.blp',
    'session/content/message_row/text.blp',
    'session/content/mod.blp',
    'session/content/message_menu.blp',
    'session/content/send_media_window.blp',
    'session/mod.blp',
    'session/preferences_window.blp',
    'session/sidebar/avatar.blp',
    'session/sidebar/row.blp',
    'session/sidebar/row_menu.blp',
    'session/sidebar/session_switcher/add_account_row.blp',
    'session/sidebar/session_switcher/avatar_with_selection.blp',
    'session/sidebar/session_switcher/mod.blp',
    'session/sidebar/session_switcher/session_entry_row.blp',
  ),
  output: '.',
  command: [find_program('blueprint-compiler'), 'batch-compile', '@OUTPUT@', '@CURRENT_SOURCE_DIR@', '@INPUT@'],
)

ui_resources = gnome.compile_resources(
  'ui-resources',
  'ui-resources.gresource.xml',
  gresource_bundle: true,
  source_dir: meson.current_build_dir(),
  install: true,
  install_dir: pkgdatadir,
  dependencies: blueprints,
)

global_conf = configuration_data()
global_conf.set_quoted('APP_ID', application_id)
global_conf.set_quoted('PKGDATADIR', pkgdatadir)
global_conf.set_quoted('PROFILE', profile)
global_conf.set_quoted('VERSION', version + version_suffix)
global_conf.set_quoted('GETTEXT_PACKAGE', gettext_package)
global_conf.set_quoted('LOCALEDIR', localedir)
global_conf.set('TG_API_ID', tg_api_id)
global_conf.set_quoted('TG_API_HASH', tg_api_hash)
config = configure_file(
  input: 'config.rs.in',
  output: 'config.rs',
  configuration: global_conf
)
# Copy the config.rs output to the source directory.
run_command(
  'cp',
  meson.project_build_root() / 'src' / 'config.rs',
  meson.project_source_root() / 'src' / 'config.rs',
  check: true
)

cargo_options = [ '--manifest-path', meson.project_source_root() / 'Cargo.toml' ]
cargo_options += [ '--target-dir', meson.project_build_root() / 'src' ]

if get_option('profile') == 'default'
  cargo_options += [ '--release' ]
  rust_target = 'release'
  message('Building in release mode')
else
  rust_target = 'debug'
  message('Building in debug mode')
endif

cargo_env = [ 'CARGO_HOME=' + meson.project_build_root() / 'cargo-home' ]

custom_target(
  'cargo-build',
  build_by_default: true,
  build_always_stale: true,
  output: meson.project_name(),
  console: true,
  install: true,
  install_dir: bindir,
  depends: resources,
  command: [
    'env',
    cargo_env,
    cargo, 'build',
    cargo_options,
    '&&',
    'cp', 'src' / rust_target / meson.project_name(), '@OUTPUT@',
  ]
)

cargo = find_program('cargo', required: true)
cargo_target_dir = meson.project_build_root() / 'target'
cargo_home = meson.project_build_root() / 'cargo-home'
manifest_path = meson.project_source_root() / 'Cargo.toml'
test (
  'clippy',
  cargo,
  args: [
    'clippy',
    '--manifest-path=@0@'.format(manifest_path),
    '--target-dir=@0@'.format(cargo_target_dir),
    '--',
    '-D',
    'warnings',
  ],
  env: [
    'CARGO_HOME=@0@'.format(cargo_home),
    'PATH=/app/bin:/usr/bin:/usr/lib/sdk/llvm16/bin:/usr/lib/sdk/rust-stable/bin',
  ],
  timeout: 300, # Give cargo more time
)
